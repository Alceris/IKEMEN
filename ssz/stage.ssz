

lib file = <file.ssz>;
lib s = <string.ssz>;
lib mat = <math.ssz>;
lib consts = <consts.ssz>;
lib sff = "sff.ssz";
lib com = "common.ssz";
lib bg = "bg.ssz";
lib cfg = "config.ssz";

public ^&com.PalFX bgPalFX;
bgPalFX.new(1);

&EnvShake
{
  public int time;
  public float freq;
  public float ampl;
  public float phase;
  new()
  {
    `clear();
  }
  public void clear()
  {
    `time = 0;
    `freq = .mat.PI / 3.0;
    `ampl = -4.0;
    `phase = .com.FERR;
  }
  public void setDefPhase()
  {
    if(`phase != .com.FERR) ret;
    `phase = (`freq >= .mat.PI/2.0 ? .mat.PI/2.0 : 0.0);
  }
  public void next()
  {
    if(`time <= 0) ret;
    `time--;
    `phase += `freq;
  }
  public float getOffset()
  {
    if(`time <= 0) ret 0.0;
    ret `ampl * .mat.sin(`phase);
  }
}
public &EnvShake envShake;

&Camera
{
  public int startx = 0;
  public int boundleft = -95, boundright = 95;
  public int boundhigh = -25;
  public float verticalfollow = 0.2;
  public int tension = 50, floortension = 0;
}

&Player
{
  public int startx = -70, starty = 0;
}

&Shadow
{
  public ^uint pal;
  public int intensity = 128;
  public uint color = 0x0;
  public float yscale = 0.4;
  public int fadeend = .consts.int_t::MIN, fadebgn = .consts.int_t::MIN;
  new()
  {
    `pal.new(256);
    loop{index i = 0; do: `pal[i] = `color; while ++i < 256:}
  }
}

public &Stage
{
  public ^/char spr;
  public ^/char bgmusic;
  public ^/char name;
  public ^&.sff.Sff sf;
  public %&.bg.BackGround bg;
  public %&.bg.Action actionList;
  public %&.bg.BGCtrl bgctrlList;
  public &.bg.BGCTimeLine bgctl;
  public &.bg.BGCtrl bgcdef;
  public &.bg.BGAction bga;
  public &.Camera cam;
  public &.Shadow sdw;
  public &.Player p1, p2;
  public int screenleft = 15, screenright = 15;
  public int zoffset = 0;
  public int zoffsetlink = 0;
  public float ztopscale = 1.0;
  public int reflection = 0;
  public bool hires = false, resetbg = true, debugbg = false, reflect = true;
  new()
  {
    `p2.startx *= -1;
  }
  ^&.bg.Action getAction(int no)
  {
    loop{index i = 0; while; do:
      if(`actionList[i].no == no) ret `actionList[i..i+1];
      i++;
    while i< #`actionList:
    }
    ret .consts.null!&.bg.Action?();
  }
  void getBG(%^&.bg.BackGround bgl=, int id)
  {
    loop{index i = 0; while; do:
      if(`bg[i].id == id) bgl.new[-1] = `bg[i..i+1];
      i++;
    while i < #`bg:
    }
  }
  public bool load(^/char def)
  {:<-
    public void info(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("name")) > 0) `name = data;
    }
    public void camera(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("startx")) > 0) `cam.startx = .com.atoi(data);
      if(#(data = sc.get("boundleft")) > 0){
        `cam.boundleft = .com.atoi(data);
      }
      if(#(data = sc.get("boundright")) > 0) `cam.boundright = .com.atoi(data);
      if(#(data = sc.get("boundhigh")) > 0) `cam.boundhigh = .com.atoi(data);
      if(#(data = sc.get("verticalfollow")) > 0){
        `cam.verticalfollow = .com.atof(data);
      }
      if(#(data = sc.get("tension")) > 0) `cam.tension = .com.atoi(data);
      if(#(data = sc.get("floortension")) > 0){
        `cam.floortension = .com.atoi(data);
      }
    }
    public void playerinfo(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("p1startx")) > 0) `p1.startx = .com.atoi(data);
      if(#(data = sc.get("p1starty")) > 0) `p1.starty = .com.atoi(data);
      if(#(data = sc.get("p2startx")) > 0) `p2.startx = .com.atoi(data);
      if(#(data = sc.get("p2starty")) > 0) `p2.starty = .com.atoi(data);
    }
    public void scaling(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("topscale")) > 0) `ztopscale = .com.atof(data);
    }
    public void bound(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("screenleft")) > 0) `screenleft = .com.atoi(data);
      if(#(data = sc.get("screenright")) > 0) `screenright = .com.atoi(data);
    }
    public void stageinfo(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("zoffset")) > 0) `zoffset = .com.atoi(data);
      if(#(data = sc.get("zoffsetlink")) > 0) `zoffsetlink = .com.atoi(data);
      if(#(data = sc.get("hires")) > 0) `hires = .com.atoi(data) != 0;
      if(#(data = sc.get("resetbg")) > 0) `resetbg = .com.atoi(data) != 0;
    }
    public void shadow(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("intensity")) > 0){
        `sdw.intensity =
          .mat.max!int?(0)<, .mat.min!int?(255)<, .com.atoi(data);
      }
      if(#(data = sc.get("color")) > 0){
        ^ubyte c = .com.csvToAry!c<>.self?(data);
        if(#c >= 3) `sdw.color = c[0]<<0d16 | c[1]<<0d8 | c[2];
        loop{index i = 0; do:
          `sdw.pal[i] = `sdw.color;
        while ++i < 256:
        }
      }
      if(#(data = sc.get("yscale")) > 0) `sdw.yscale = .com.atof(data);
      if(#(data = sc.get("reflect")) > 0) `reflect = .com.atoi(data) != 0;
      if(#(data = sc.get("fade.range")) > 0){
        .com.readPair!int?(`sdw.fadeend=, `sdw.fadebgn=, data);
      }
    }
    public void reflection(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("intensity")) > 0){
        `reflection = .mat.max!int?(0)<, .mat.min!int?(255)<, .com.atoi(data);
      }
    }
    public void music(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("bgmusic")) > 0) `bgmusic = .com.readFileName(data);
    }
    public void bgdef(&.com.Section sc=)
    {
      ^/char data;
      if(#(data = sc.get("spr")) > 0) `spr = .com.readFileName(data);
      if(#(data = sc.get("debugbg")) > 0) `debugbg = .com.atoi(data) != 0;
    }
    public void bg(&.com.Section sc=, index link=)
    {
      if(#`bg > 0 && !`bg[-1].positionlink) link = #`bg - 1;
      `bg.new[-1];
      `bg[-1].read(sc=, `hires, `bg[link..link+1]);
    }
    public void begin(^^/char lines, index i=, ^/char section)
    {
      index fnd = .s.cFind(" "\t, section);
      ^/char name = .s.toLower()<, section[0..fnd];
      if(fnd >= 0) loop{while; do:
        fnd++;
      while fnd < #section && .s.cMatch!char?(" "\t, section[fnd]):
      }
      ^/char sec = section[fnd..-1];
      branch{
      cond .s.equ(name, "action"):
        {
          int no;
          no = .com.atoi(sec);
          ^&.bg.Action act = `getAction(no);
          if(#act > 0) break;
          `actionList.new[-1].no = no;
          act = `actionList[#`actionList-1..-1];
          act~read(lines, i=);
        }
      }
    }
    public void bgctrldef(&.com.Section sc=)
    {
      `bgcdef.ctrlbg.new(0);
      `bgcdef.looptime = -1;
      `bgcdef.positionlink = false;
      loop{index i = 0; while; do:
        `bgcdef.ctrlbg.new[-1] = `bg[i..i+1];
        i++;
      while i < #`bg:
      }
      ^/char data;
      if(#(data = sc.get("ctrlid")) > 0){
        ^int ids = .com.csvToAry!ids<>.self?(data);
        `bgcdef.ctrlbg.new(0);
        branch{
        cond #ids == 1 && ids[0] == -1:
          loop{index i = 0; while; do:
            `bgcdef.ctrlbg.new[-1] = `bg[i..i+1];
            i++;
          while i < #`bg:
          }
        else:
          loop{
            index i = 0;
            while;
          do:
            loop{index j = 0; while; do:
              if(ids[j] == ids[i]) break, continue;
              j++;
            while j < i:
            }
            `getBG(`bgcdef.ctrlbg=, ids[i]);
          continue:
            i++;
          while i < #ids:
          }
        }
      }
      if(#(data = sc.get("looptime")) > 0) `bgcdef.looptime = .com.atoi(data);
    }
    public void bgctrl(&.com.Section sc=)
    {
      if(#`bgctrlList == 0 && #`bgcdef.ctrlbg == 0) loop{
        index i = 0;
        while;
      do:
        `bgcdef.ctrlbg.new[-1] = `bg[i..i+1];
        i++;
      while i < #`bg:
      }
      `bgctrlList.new[-1].ctrlbg = `bgcdef.ctrlbg;
      `bgctrlList[-1].looptime = `bgcdef.looptime;
      ^/char data;
      if(#(data = sc.get("ctrlid")) > 0){
        ^int ids = .com.csvToAry!ids<>.self?(data);
        `bgctrlList[-1].ctrlbg.new(0);
        branch{
        cond #ids == 1 && ids[0] == -1:
          loop{index i = 0; while; do:
            `bgctrlList[-1].ctrlbg.new[-1] = `bg[i..i+1];
            i++;
          while i < #`bg:
          }
        else:
          loop{
            index i = 0;
            while;
          do:
            loop{index j = 0; while; do:
              if(ids[j] == ids[i]) break, continue;
              j++;
            while j < i:
            }
            `getBG(`bgctrlList[-1].ctrlbg=, ids[i]);
          continue:
            i++;
          while i < #ids:
          }
        }
      }
      `bgctrlList[-1].read(sc=);
      if(`bgctrlList[-1].ctrlbg == `bgcdef.ctrlbg){
        `bgctrlList[-1].positionlink |= `bgcdef.positionlink;
      }
    }
    void section(^^/char lines, index i=, index link=)
    {
      ^/char sec = lines[i];
      ^/char secname = .com.sectionName(sec=);
      if(#secname == 0) ret;
      i++;
      if(.s.equ(secname, "begin")){
        `load:<-begin(lines, i=, sec);
        ret;
      }
      &.com.Section sc.parse(lines, i=);
      branch{
      cond .s.equ(secname, "info"):
        `load:<-info(sc=);
      cond .s.equ(secname, "camera"):
        `load:<-camera(sc=);
      cond .s.equ(secname, "playerinfo"):
        `load:<-playerinfo(sc=);
      cond .s.equ(secname, "scaling"):
        `load:<-scaling(sc=);
      cond .s.equ(secname, "bound"):
        `load:<-bound(sc=);
      cond .s.equ(secname, "stageinfo"):
        `load:<-stageinfo(sc=);
      cond .s.equ(secname, "shadow"):
        `load:<-shadow(sc=);
      cond .s.equ(secname, "reflection"):
        `load:<-reflection(sc=);
      cond .s.equ(secname, "music"):
        `load:<-music(sc=);
      cond .s.equ(secname, "bgdef"):
        `load:<-bgdef(sc=);
      cond .s.equ(secname, "bg"):
        `load:<-bg(sc=, link=);
      cond .s.equ(secname, "bgctrldef"):
        `load:<-bgctrldef(sc=);
      cond .s.equ(secname, "bgctrl"):
        `load:<-bgctrl(sc=);
      }
    }
    ^char buf = .file.loadAsciiText(def);
    ^^/char lines = .com.splitLines(buf);
    .s.each!^/char?([void(^/char l=){l=.s.trim(l);}], lines);
    `clear();
    loop{
      index i = 0, link = -1;
      while;
    do:
      section(lines, i=, link=);
      i++;
    while i < #lines:
    }
    if(!`reflect) `reflection = 0;
    `sf.new(1);
    if(
      !.com.loadFile(def, `spr=)<,
      [bool(^/char f){ret `sf~loadFile(f, false);}]) ret false;
    loop{
      index i = 0, link = 0;
      int zofs = 0;
    while; do:
      `bg[i].setup(`sf, `hires, (float)`cam.startx)<,
        [void(^&.bg.Action act=, int n){act = `getAction(n);}];
      branch{
      cond `bg[i].positionlink && i > 0:
        `bg[i].startx += `bg[link].startx;
        `bg[i].starty += `bg[link].starty;
      else:
        link = i;
      }
      if(`zoffsetlink > 0 && `bg[i].id == `zoffsetlink){
        zofs = (int)`bg[i].starty;
      }
      i++;
    while i < #`bg:
      `zoffset += zofs;
    }
    ret true;
  }
  type bglsig_t = $void(&.bg.BackGround=);
  public void bgCtrl(&.bg.BGCtrl bgc=)
  {:<-
    void bgLoop(^^&.bg.BackGround bglist, ~`bglsig_t dg)
    {
      loop{index i = 0; while; do:
        dg(:bglist[i]<>=:);
        i++;
      while i < #bglist:
      }
    }
    bgc.currenttime++;
    switch(bgc.typ){
    case .bg.BgcType::Anim:
      {
        ^&.bg.Action act = `getAction((int)bgc.x);
        if(#act > 0) bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
          bg.actionno = (int)bgc.x;
          bg.anim.setFrames(act~frames, act~loopstart, act~nrepeat);
          bg.anim.current = 0;
        }];
      }
    case .bg.BgcType::Visible:
      bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
        bg.visible = (int)bgc.x != 0;
      }];
    case .bg.BgcType::Enable:
      bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
        bg.visible = bg.active = (int)bgc.x != 0;
      }];
    case .bg.BgcType::PosSet:
      bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
        if(bgc.setx) bg.bga.x = bgc.x;
        if(bgc.sety) bg.bga.y = bgc.y;
      }];
      if(bgc.positionlink){
        if(bgc.setx) `bga.x = bgc.x;
        if(bgc.sety) `bga.y = bgc.y;
      }
    case .bg.BgcType::PosAdd:
      bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
        if(bgc.setx) bg.bga.x += bgc.x;
        if(bgc.sety) bg.bga.y += bgc.y;
      }];
      if(bgc.positionlink){
        if(bgc.setx) `bga.x += bgc.x;
        if(bgc.sety) `bga.y += bgc.y;
      }
    case .bg.BgcType::SinX:
      bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
        bg.bga.xradius = bgc.x;
        bg.bga.sinxlooptime = (int)bgc.y;
        if(bg.bga.sinxlooptime > 0){
          bg.bga.sinxtime =
            (int)((float)bgc.n*bgc.y/360.0) % bg.bga.sinxlooptime;
          if(bg.bga.sinxtime < 0) bg.bga.sinxtime += bg.bga.sinxlooptime;
        }
      }];
      if(bgc.positionlink){
        `bga.xradius = bgc.x;
        `bga.sinxlooptime = (int)bgc.y;
        if(`bga.sinxlooptime > 0){
          `bga.sinxtime = (int)((float)bgc.n*bgc.y/360.0) % `bga.sinxlooptime;
          if(`bga.sinxtime < 0) `bga.sinxtime += `bga.sinxlooptime;
        }
      }
    case .bg.BgcType::SinY:
      bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
        bg.bga.yradius = bgc.x;
        bg.bga.sinylooptime = (int)bgc.y;
        if(bg.bga.sinylooptime > 0){
          bg.bga.sinytime =
            (int)((float)bgc.n*bgc.y/360.0) % bg.bga.sinylooptime;
          if(bg.bga.sinytime < 0) bg.bga.sinytime += bg.bga.sinylooptime;
        }
      }];
      if(bgc.positionlink){
        `bga.yradius = bgc.x;
        `bga.sinylooptime = (int)bgc.y;
        if(`bga.sinylooptime > 0){
          `bga.sinytime = (int)((float)bgc.n*bgc.y/360.0) % `bga.sinylooptime;
          if(`bga.sinytime < 0) `bga.sinytime += `bga.sinylooptime;
        }
      }
    case .bg.BgcType::VelSet:
      bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
        if(bgc.setx) bg.bga.vx = bgc.x;
        if(bgc.sety) bg.bga.vy = bgc.y;
      }];
      if(bgc.positionlink){
        if(bgc.setx) `bga.vx = bgc.x;
        if(bgc.sety) `bga.vy = bgc.y;
      }
    case .bg.BgcType::VelAdd:
      bgLoop(bgc.ctrlbg)<, [`bglsig_t(bg=){
        if(bgc.setx) bg.bga.vx += bgc.x;
        if(bgc.sety) bg.bga.vy += bgc.y;
      }];
      if(bgc.positionlink){
        if(bgc.setx) `bga.vx += bgc.x;
        if(bgc.sety) `bga.vy += bgc.y;
      }
    }
  }
  public void action()
  {
    `bgctl.step!`self?(`=);
    `bga.action();
    loop{
      index i = 0, link = 0;
      float yofs = 0.0;
      while;
    do:
      `bg[i].bga.action();
      branch{
      cond i > 0 && `bg[i].positionlink:
        `bg[i].bga.xoffset += `bg[link].bga.sinxoffset;
        `bg[i].bga.yoffset += `bg[link].bga.sinyoffset;
      else:
        link = i;
      }
      if(`zoffsetlink > 0 && `bg[i].id == `zoffsetlink){
        yofs = `bg[i].bga.yoffset;
      }
      if(`bg[i].active) `bg[i].anim.action();
      i++;
    while i < #`bg:
      `bga.yoffset += yofs;
    }
  }
  public void bgDraw(bool t, float x, float y, float scl)
  {
    loop{index i = 0; while; do:
      if(`bg[i].visible && `bg[i].toplayer == t && #`bg[i].anim.spr > 0){
        `bg[i].draw(
          x, y + .envShake.getOffset(),
          scl, .bgPalFX~time != 0 ? .bgPalFX : .com.allPalFX);
      }
      i++;
    while i < #`bg:
    }
  }
  public void clear()
  {
    `spr = "";
    `bgmusic = "";
    `name = "";
    `bg.new(0);
    `actionList.new(0);
    `bgctrlList.new(0);
  }
  public void reset()
  {
    `bga.clear();
    loop{index i = 0; while; do:
      `bg[i].reset();
      i++;
    while i < #`bg:
    }
    loop{index i = 0; while; do:
      `bgctrlList[i].currenttime = 0;
      i++;
    while i < #`bgctrlList:
    }
    `bgctl.clear();
    loop{index i = #`bgctrlList-1; while; do:
      `bgctl.add(`bgctrlList[i..i+1]);
      i--;
    while i >= 0:
    }
  }
}


