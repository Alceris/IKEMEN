
lib s = <string.ssz>;
lib consts = <consts.ssz>;
lib m = <math.ssz>;
lib sdl = <alpha/sdlplugin.ssz>;
lib cfg = "config.ssz";
lib mes = <alpha/mesdialog.ssz>;
lib tbl = <table.ssz>;
lib file = <file.ssz>;
lib se = <alpha/sdlevent.ssz>;


//チームモードの種類
|TeamMode
{
  Single, Simul, Turns
}

//Simulでのパワーの共有フラグ
public bool powershare = true;

//コリジョン表示フラグ
public bool clsndraw = false;

//デバッグ表示フラグ
public bool debugdraw = false;

public ^%%char clipboardText.new(4);

public ^int com = com + (int)[4, 4, 4, 4];

public bool pause = false;
public float accel = 1.0;

public bool autolevel = false;

const int GameWidth =
  cfg.Width*3 > cfg.Height*4 ? cfg.Width*3*320/(cfg.Height*4) : 320; 
const int GameHeight =
  cfg.Height*4 > cfg.Width*3 ? cfg.Height*4*240/(cfg.Width*3) : 240; 
const float WidthScale = (float)cfg.Width / (float)GameWidth;
const float HeightScale = (float)cfg.Height / (float)GameHeight;

public &sdl.Rect scrrect;
scrrect.set(0, 0, (ushort)cfg.Width, (ushort)cfg.Height);


const float Speed = (float)cfg.GameSpeed / 60.0;

const float P1P3Dist = 25.0;

const int IERR = consts.int_t::MIN;


/int tickCount = 0;
int oldTickCount = tickCount-1;
float tickCountF = 0.0, lastTick = 0.0;
float nextAddTime = Speed;

const bool tickFrame()
{
  ret .oldTickCount < .tickCount;
}

const bool tickNextFrame()
{
  ret (int)(.tickCountF + .nextAddTime) > .tickCount;
}

const float tickInterpola()
{
  ret .tickNextFrame() ? 1.0 : .tickCountF - .lastTick + .nextAddTime;
}

public bool addFrameTime(float t)
{
  branch{
    .oldTickCount = .tickCount;
  cond (int).tickCountF > .tickCount:
    .tickCount++;
    ret false;
  cond (int)(.tickCountF += .nextAddTime) > .tickCount:
    .tickCount++;
    .lastTick = .tickCountF;
  }
  .nextAddTime = t;
  ret true;
}


public int brightness = 256;

public float screenleft = 0.0, screenright = 0.0;
public float xmin = 0.0, xmax = 0.0;
public float camerax = 0.0, cameray = 0.0;
public float screenx = 0.0, screeny = 0.0;
public float scrxmin = 0.0, scrxmax = 0.0;
public float zoffset = 0.0;
public float yoffset = 0.0;
public float xoffset = 0.0;
public float scale = 1.0;
public float turbo = Speed;
public int gametime = 0, time = 0;
public int intro = 20;

//ホームチーム 1P:0, 2P:1
public int home = 0;

public int p1mw = 2, p2mw = 2;

//ラウンドNo.
public int round = 1;

//チームモード
public ^int tmode;
tmode.new(2);
tmode[0] = tmode[1] = (int)TeamMode::Single;

//Turnsの人数
public ^int numturns;
numturns.new(2);
numturns[0] = numturns[1] = 2;

public ^int rexisted;
rexisted.new(2);
rexisted[0] = rexisted[1] = 0;

public int p1wins = 0, p2wins = 0, draws = 0;

public int win = -1;

|KeyEvent
{
  CtrlC, CtrlD, CtrlS, Pause, Scroll, F4, ShiftF4,

  NumEvents
}

se.eventKeys.new((int)KeyEvent::NumEvents);
se.eventKeys[(int)KeyEvent::CtrlC].key = .sdl.SDLKey::c;
se.eventKeys[(int)KeyEvent::CtrlC].ctrl = true;
se.eventKeys[(int)KeyEvent::CtrlD].key = .sdl.SDLKey::d;
se.eventKeys[(int)KeyEvent::CtrlD].ctrl = true;
se.eventKeys[(int)KeyEvent::CtrlS].key = .sdl.SDLKey::s;
se.eventKeys[(int)KeyEvent::CtrlS].ctrl = true;
se.eventKeys[(int)KeyEvent::Pause].key = .sdl.SDLKey::PAUSE;
se.eventKeys[(int)KeyEvent::Scroll].key = .sdl.SDLKey::SCROLLOCK;
se.eventKeys[(int)KeyEvent::F4].key = .sdl.SDLKey::F4;
se.eventKeys[(int)KeyEvent::ShiftF4].key = .sdl.SDLKey::F4;
se.eventKeys[(int)KeyEvent::ShiftF4].shift = true;

public bool keyDown(|.KeyEvent k)
{
  ret .se.eventKeys[(int)k].down;
}


public int nextLine(index i=, ^/char str)
{
  loop{while; do:
    branch{
    cond str[i] == '\n':
      ret 1;
    cond str[i] == '\r':
      if(i+1 < #str && str[i+1] == '\n'){
        i++;
        ret 2;
      }
    break:
      i++;
    }
  while i < #str:
  }
  ret 0;
}

public %^/char splitLines(^/char str)
{
  %^/char spl;
  loop{index i = 0; do:
    spl.new[-1] = str[i..-.nextLine(i=, str)+i];
  while i < #str:
  }
  ret spl;
}

public double atof(^/char str)
{
  ^/char s = .s.trim(str);
  if(#s == 0) ret 0.0;
  bool mi = s[0] == '-';
  index i = (int)mi, p = 0;
  double f = 0.0;
  loop{
    while;
  do:
    branch{
    cond s[i] >= '0' && s[i] <= '9':
      f = f*10.0 + (double)(s[i] - '0');
    cond s[i] == '.':
      if(p > 0) else;
      p = i + 1;
    else:
      break, break;
    }
    i++;
  while i < #s:
  }
  if(p > 0) f *= 10.0**-(double)(i - p);
  if(mi) f = -f;
  ret f;
}

public long atoi(^/char str)
{
  ret (long).atof(str);
}

public ..csvToAry<_t>(^/char csv) ^_t
{
  ^^/char spl = .s.split(",", csv);
  ^_t ary.new(#spl);
  loop{index i = 0; while; do:
    ary[i] = (_t).atof(spl[i]);
    i++;
  while i < #ary:
  }
  ret ary;
}

public &IXY
{
  public int x = .IERR, y = .IERR;
  public void set(int x, int y)
  {
    `x = x;
    `y = y;
  }
}

public &FXY
{
  public float x = 0.0/0.0, y = 0.0/0.0;
  public void set(float x, float y)
  {
    `x = x;
    `y = y;
  }
}

public void readPair<_t>(_t x=, _t y=, ^/char data)
{
  ^_t xy = .csvToAry!_t?(data);
  x = xy[0];
  if(#xy >= 2) y = xy[1];
}


public &Layout
{
  public &.FXY offset;
  public int displaytime = -2;
  public byte facing = 1;
  public byte vfacing = 1;
  public short layerno = 0;
  public &.FXY scale;
  new()
  {
    `offset.set(0.0, 0.0);
    `scale.set(1.0, 1.0);
  }
  public void read(^/char img, &.Section sc=)
  {
    ^/char data;
    if(#(data = sc.benri(img, "offset")) > 0){
      .readPair!float?(`offset.x=, `offset.y=, data);
    }
    if(#(data = sc.benri(img, "displaytime")) > 0){
      `displaytime = .atoi(data);
    }
    if(#(data = sc.benri(img, "facing")) > 0){
      `facing = .atoi(data) < 0 ? -1: 1;
    }
    if(#(data = sc.benri(img, "vfacing")) > 0){
      `vfacing = .atoi(data) < 0 ? -1: 1;
    }
    if(#(data = sc.benri(img, "layerno")) > 0){
      `layerno = .m.min!int?(2)<, .atoi(data);
    }
    if(#(data = sc.benri(img, "scale")) > 0){
      ^float xy = .csvToAry!float?(data);
      `scale.x = xy[0];
      `scale.y = #xy >= 2 ? xy[1] : 1.0;
    }
  }
  public void setup()
  {
    if(`facing < 0) `offset.x += .cfg.LifebarFontScale;
    if(`vfacing < 0) `offset.y += .cfg.LifebarFontScale;
  }
}

public ^char readFileName(^/char f)
{
  ^char tmp;
  .mes.AsciiToLocal(:tmp=, f:);
  ret tmp;
}

public void error<_t>(^/char m)
{
  lib al = <alert.ssz>;
  al.alert!_t?(m);
}

public ^uint tmppl.new(256);
/?/*!.cfg.OpenGL:
^ubyte px.new(1);
px<> = 0x0;
/*?*/
public void rectFill(.scrrect.self rect=, uint color, int alpha)
{
/?/*.cfg.OpenGL:
  .sdl.MugenFillGl(:rect, color, alpha:);
/*true:
  &.sdl.Rect onedot.set(0, 0, 0d1, 0d1), til.set(0, 0, 0d0, 0d0);
  %byte dammy;
  .tmppl<> = color;
  .sdl.screen.renderMugenZoom(
    rect=, 0.0, 0.0, .px, .tmppl, -1, onedot=, 0.0, 0.0, til=,
    (float).cfg.Width, (float).cfg.Width, (float).cfg.Height, 0.0, 0d0,
    alpha, 0, dammy=);
/*?*/
}

public void screenFill(uint color)
{
/?/*.cfg.OpenGL:
  .rectFill(.scrrect=, color, 255);
/*true:
  .sdl.screen.fill(.scrrect=, color);
/*?*/
}

^uint workpal;
workpal.new(256);
public &PalFX
{
  public int player;
  public int time;
  public float mulr;
  public float mulg;
  public float mulb;
  public int addr;
  public int addg;
  public int addb;
  public int amplr;
  public int amplg;
  public int amplb;
  public int cycletime;
  public int sintime;
  public float color;
  public int invertall;
  new()
  {
    `clear();
  }
  public void clear()
  {
    `player = -1;
    `time = 0;
    `mulr = 1.0;
    `mulg = 1.0;
    `mulb = 1.0;
    `addr = 0;
    `addg = 0;
    `addb = 0;
    `amplr = 0;
    `amplg = 0;
    `amplb = 0;
    `cycletime = 0;
    `sintime = 0;
    `color = 1.0;
    `invertall = 0;
  }
  public void step()
  {
    if(`time > 0) `time--;
    if(`time != 0 && `cycletime > 0) `sintime = (`sintime+1) % `cycletime;
  }
  public ^uint getFxPal(^uint pal, bool neg)
  {
    void addsubset(uint addc=, uint subc=, int r, int g, int b)
    {
      int ar = 0, ag = 0, ab = 0, sr = 0, sg = 0, sb = 0;
      void bar(int a=, int s=, int c)
      {
        branch{
        cond c < 0:
          s = .m.min!int?(255, -c);
        else:
          a = .m.min!int?(255, c);
        }
      }
      branch{
      cond `neg:
        bar(sr=, ar=, r); bar(sg=, ag=, g); bar(sb=, ab=, b);
      else:
        bar(ar=, sr=, r); bar(ag=, sg=, g); bar(ab=, sb=, b);
      }
      addc = (uint)(ab | ag<<8 | ar<<16);
      subc = (uint)(sb | sg<<8 | sr<<16);
    }
    loop{
      index i = 0;
      uint c;
      float ac;
      float mr, mg, mb;
      branch{
      cond neg:
        mr = (`mulg + `mulb) * 0.5;
        mg = (`mulr + `mulb) * 0.5;
        mb = (`mulr + `mulg) * 0.5;
      else:
        mr = `mulr;
        mg = `mulg;
        mb = `mulb;
      }
      .m.limRange!float?(mr=, 0.0, 2.0);
      .m.limRange!float?(mg=, 0.0, 2.0);
      .m.limRange!float?(mb=, 0.0, 2.0);
      int adr = `addr, adg = `addg, adb = `addb;
      if(`cycletime >= 2){
        float sin =
          .m.sin(
            (
              .m.PI*2.0*(double)`sintime + (neg ? .m.PI : 0.0)
              + (`cycletime == 2 ? .m.PI/2.0 : 0.0)) / (double)`cycletime);
        adr += (int)(sin*(float)`amplr);
        adg += (int)(sin*(float)`amplg);
        adb += (int)(sin*(float)`amplb);
      }
      uint addc, subc;
      addsubset(
        addc=, subc=, (int)((float)adr*mr),
        (int)((float)adg*mg), (int)((float)adb*mb));
      uint tmp, msk;
    do:
      c = pal[i];
      if(`invertall != 0) c = !c;
      ac = (float)((c&0xff)+(c>>0d8&0xff)+(c>>0d16&0xff)) * (1.0/3.0);
      tmp = (uint)(((float)(c&0xff) + (ac-(float)(c&0xff))*(1.0-`color)) * mb);
      tmp =
        ((tmp | (uint)(((int)tmp<<23)>>31)) & 0xff)
        | (
          (uint)(
            (
              (float)(c>>0d8&0xff)
              + (ac-(float)(c>>0d8&0xff))*(1.0-`color))*mg) << 0d8);
      tmp =
        ((tmp | (uint)(((int)(tmp&0x10000)<<15)>>23)) & 0xffff)
        | (
          (uint)(
            (
              (float)(c>>0d16&0xff)
              + (ac-(float)(c>>0d16&0xff))*(1.0-`color))*mr) << 0d16);
      c = tmp | (uint)(((int)(tmp&0x1000000)<<7)>>15);
      tmp = ((c & addc) + (((c ^ addc) >> 0d1) & 0x7f7f7f7f)) & 0x80808080;
      msk = (tmp << 0d1) - (tmp >> 0d7);
      c = ((c + addc) - msk) | msk;
      tmp = (((!c & subc) << 0d1) + ((!c ^ subc) & 0xfefefefe)) & 0x01010100;
      msk = tmp - (tmp >> 0d8);
      c = (c - subc + tmp) & !msk;
      .workpal[i] = c;
    while ++i < 256:
    }
    ret .workpal;
  }
}

public ^&PalFX allPalFX;
allPalFX.new(1);


public bool loadFile(^/char deffile, ^/char file=, ~$bool(^/char) load)
{
  loop{index i = #deffile-1; while; do:
    if(.s.cMatch!char?("\/", deffile[i])){
      %char buf;
      buf .= deffile[0..i+1];
      buf .= file;
      if(load(:buf:)){
        file = buf;
        break, break, break;
      }
    }
    i--;
  while i >= 0:
    {
      %char buf;
      buf .= "data/";
      buf .= file;
      if(load(:buf:)){
        file = buf;
        break, break, break;
      }
      buf.new(0);
      buf .= "ssz/";
      buf .= file;
      if(load(:buf:)){
        file = buf;
        break, break, break;
      }
    }
    if(!load(:file:)) ret false;
  }
  ret true;
}

public &Section
{
  &.tbl.NameTable!char? params;
  public void parse(^^/char lines, index i=)
  {
    loop{
      index f;
      ^/char line;
      while;
    do:
      if(#lines[i] > 0 && lines[i][0] == '['){
        if(.s.find("]", lines[i]) < 0) break, continue;
        i--;
        break, break;
      }
      line = lines[i][0...s.find(";", lines[i])];
      f = .s.find("=", line);
      if(f > 0){
        index foo = 0;
        loop{while; do:
          foo++;
        while .s.cMatch!char?(" "\t, line[foo]):
        }
        foo += .s.cFind("= "\t, line[foo..-1]);
        ^/char name = .s.toLower()<, .s.trim()<, line[0..foo];
        if(#name > 0){
          ^/char data = .s.trim(line[f+1..-1]);
          if(#data == 0) break;
          if(#`params.get(name) == 0) `params.set(name, .s.clone!char?(data));
        }
      }
    continue:
      i++;
    while i < #lines:
    }
  }
  public ^/char get(^/char name)
  {
    ret `params.get(name);
  }
  public ^/char getText(^/char name)
  {
    ^/char data = `params.get(name);
    if(#data > 0 && data[0] == '"') data = data[1...s.find(\", data[1..-1])+1];
    ret data;
  }
  public ^/char benri(^/char head, ^/char name)
  {
    %char buf;
    ret `params.get(buf .= head .= name);
  }
}

public ^/char sectionName(^/char sec=)
{
  if(#sec == 0 || sec[0] != '[') ret "";
  sec = sec[0.. .s.find(";", sec)];
  index f = .s.find("]", sec);
  if(f < 0) ret "";
  sec = sec[1..f];
  index fnd = .s.cFind(" ", sec);
  if(fnd < 0) fnd = -2;
  ^/char name = .s.toLower(sec[0..fnd+1]);
  sec = sec[fnd+1..-1];
  ret name;
}


public &SuperDangerousRef
{
  public index p, pos, l;
  public void copyRef<_t>(^_t r=)
  {
    .mes.veryUnsafeCopy!`self, ^_t?(`=, r=);
  }
  public void copyList<_t>(%_t r=)
  {
    .mes.veryUnsafeCopy!`self, %_t?(`=, r=);
  }
  public void copyToRef<_t>(^_t r=)
  {
    .mes.veryUnsafeCopy!^_t, `self?(r=, `=);
  }
  public void copyToList<_t>(%_t r=)
  {
    .mes.veryUnsafeCopy!%_t, `self?(r=, `=);
  }
}

